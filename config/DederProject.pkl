module ba.sake.deder.config.DederProject

// TODO write tests https://pkl-lang.org/blog/testing-in-pkl.html#writing-tests

modules: Listing<DederModule>(modulesValidation)

hidden modulesValidation = (value: Listing<DederModule>) ->
  if (!(value.toList().map((m) -> m.id).distinct.length == value.length))
    throw("Module ids must be unique.")
  else
    true

abstract class DederModule {
  /// Module id, used in CLI and referring from other modules
  id: String(idValidation)

  /// Module root directory, relative to project root
  root: String(rootValidation) = id

  /// Source directories
  sources: Listing<String> = new Listing { "src" }

  /// Module dependencies
  moduleDeps: Listing<DederModule>

  type: ModuleType

  hidden idValidation = (value: String) ->
    if (!value.matches(Regex("^[.a-zA-Z0-9_-]+$")))
      throw("Id must be alphanumeric and can contain '_' and '-' but got: " + value)
    else
      true

  hidden rootValidation = (value: String) ->
    if (!value.matches(Regex("^[.a-zA-Z0-9/_-]+$")))
      throw("Root must be alphanumeric and can contain '_', '-', and '/' but got: " + value)
    else
      true
}

typealias ModuleType = "java" | "scala" | "scala-test" | "scala-js" | "scala-native"

class PomLicense {
  name: String(!isEmpty)
  url: String(!isEmpty)
}
class PomDeveloper {
  id: String(!isEmpty)
  name: String(!isEmpty)
  email: String(!isEmpty)
}
class PomScm {
  url: String?
  connection: String?
  developerConnection: String?
  tag: String?
}
class PomSettings {
  groupId: String(!isEmpty)
  artifactId: String(!isEmpty)
  version: String(!isEmpty)
  name: String(!isEmpty)
  description: String?
  url: String?
  licenses: Listing<PomLicense> = new Listing {}
  developers: Listing<PomDeveloper> = new Listing {}
  scm: PomScm?
}

open class JavaModule extends DederModule {
  type = "java"
  resources: Listing<String> = new Listing { "resources" }
  javaHome: String?
  jvmOptions: Listing<String> = new Listing {}
  javaVersion: String?
  javacOptions: Listing<String> = new Listing {}
  mainClass: String?
  deps: Listing<String> = new Listing {}
  javacAnnotationProcessorDeps: Listing<String> = new Listing {}
  semanticdbEnabled: Boolean = read?("env:CI") != "true" // disable in CI by default
  javaSemanticdbVersion: String = "0.11.1"
  // TODO manifest entries
  publish: Boolean = true
  pomSettings: PomSettings?
}

open class ScalaModule extends JavaModule {
  type = "scala"
  scalaVersion: String(!isEmpty)
  scalacOptions: Listing<String> = new Listing {}
  scalacPluginDeps: Listing<String> = new Listing {}
  scalaSemanticdbVersion: String?
  function asTest(): ScalaTestModule = (this.toDynamic().toTyped(ScalaTestModule)) {
    type = "scala-test"
    publish = false
  }
  function asJs(): ScalaJsModule = (this.toDynamic().toTyped(ScalaJsModule)) {
    type = "scala-js"
  }
  function asNative(): ScalaNativeModule = (this.toDynamic().toTyped(ScalaNativeModule)) {
    type = "scala-native"
  }
}

class ScalaTestModule extends ScalaModule {
  type = "scala-test"
  testFrameworks: Listing<String> = new Listing {
    "com.novocode.junit.JUnitFramework"
    "com.github.sbt.junit.jupiter.api.JupiterFramework"
    "org.scalatest.tools.Framework"
    "org.specs2.runner.Specs2Framework"
    "munit.Framework"
    "utest.runner.Framework"
    "zio.test.sbt.ZTestFramework"
    "weaver.framework.CatsEffect"
  }
}

typealias ScalaJsModuleKind = "no-module" | "es-module" | "commonjs-module"

open class ScalaJsModule extends ScalaModule {
  type = "scala-js"
  scalaJsVersion: String(!isEmpty)
  moduleKind: ScalaJsModuleKind = "no-module"
}

open class ScalaNativeModule extends ScalaModule {
  type = "scala-native"
  scalaNativeVersion: String(!isEmpty)
}

// helpers
// https://pkl-lang.org/blog/class-as-a-function.html

class ScalaModules {
  main: ScalaModule
  test: ScalaTestModule
  all: Listing<DederModule> = new { main; test }
}
class CreateScalaModules {
  _root: String
  _template: ScalaModule
  _testTemplate: ScalaTestModule = _template.asTest()
  fixed get: ScalaModules = scalaModules(_template, _testTemplate, _root)
}

const function scalaModules(
  _template: ScalaModule,
  _testTemplate: ScalaTestModule,
  _root: String,
): ScalaModules = new {
  main = (_template) {
    id = _root
    root = _root
  }
  test = (_testTemplate) {
    id = "\(_root)-test"
    root = "\(_root)/test"
    moduleDeps {
      main
    }
  }
}

class CrossModules {
  jvm: ScalaModule
  jvm_test: ScalaTestModule
  js: ScalaJsModule
  native: ScalaNativeModule
  all: Listing<DederModule> = new { jvm; jvm_test; js; native }
}

class CreateCrossModules {
  _template: ScalaModule
  _testTemplate: ScalaTestModule = _template.asTest()
  _jsTemplate: ScalaJsModule = _template.asJs()
  _nativeTemplate: ScalaNativeModule = _template.asNative()
  _root: String
  fixed get: CrossModules =
    crossModules(
      _template,
      _testTemplate,
      _jsTemplate,
      _nativeTemplate,
      _root,
    )
}

const function crossModules(
  _template: ScalaModule,
  _testTemplate: ScalaTestModule,
  _jsTemplate: ScalaJsModule,
  _nativeTemplate: ScalaNativeModule,
  _root: String,
): CrossModules = new {
  jvm = (_template) {
    id = "\(_root)-jvm-\(_template.scalaVersion)"
    root = _root
    sources {
      "src-jvm"
    }
  }
  jvm_test = (_testTemplate) {
    id = "\(_root)-jvm-test-\(_template.scalaVersion)"
    root = "\(_root)/test"
    sources {
      "src-jvm"
    }
    moduleDeps {
      jvm
    }
  }
  js = (_jsTemplate) {
    id = "\(_root)-js-\(_template.scalaVersion)"
    root = _root
    sources {
      "src-js"
    }
  }
  native = (_nativeTemplate) {
    id = "\(_root)-native-\(_template.scalaVersion)"
    root = _root
    sources {
      "src-native"
    }
  }
}
